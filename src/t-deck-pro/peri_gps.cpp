
#include "utilities.h"
#include "peripheral.h"
#include <TinyGPS++.h>

#include "loop_functions.h"
#include "loop_functions_extern.h"

/* clang-format off */

extern TinyGPSPlus gps;
static bool GPS_Recovery();
bool setupGPS();
void displayInfo();

static TaskHandle_t gps_handle;
static double gps_lat=0, gps_lng=0, gps_altitude=0, gps_speed=0;
static uint16_t gps_year=0;
static uint8_t gps_month=0, gps_day=0, gps_fix=0;
static uint8_t gps_hour=0, gps_minute=0, gps_second=0;
static uint32_t gps_vsat=0;
static int gps_hdop=0;

uint8_t buffer[256];

bool gps_init(void)
{   
    bool result = false;
    // L76K GPS USE 9600 BAUDRATE
    // result = setupGPS();
    if(!result) {
        // Set u-blox m10q gps baudrate 38400
        SerialGPS.begin(38400, SERIAL_8N1, BOARD_GPS_RXD, BOARD_GPS_TXD);
        result = GPS_Recovery();
        if (!result) {
            SerialGPS.updateBaudRate(9600);
            result = GPS_Recovery();
            if (!result) {
                Serial.println("GPS Connect failed~!");
                result = false;
            }
            SerialGPS.updateBaudRate(38400);
        }
    }
    if(result) {
        Serial.println("GPS Task Create...!");
        gps_task_create();
    }
    return result;
}

void gps_task(void *param)
{
    while(1)
    {
        //while (Serial.available()) {
        //    SerialGPS.write(Serial.read());
        //}

        while (SerialGPS.available()) {
            int c = SerialGPS.read();
            // Serial.write(c);
            if (gps.encode(c)) {
                displayInfo();
            }
        }

        if (millis() > 30000 && gps.charsProcessed() < 10) {
            Serial.println(F("No GPS detected: check wiring."));
            delay(1000);
        }
        delay(1);
    }
}

void gps_task_create(void)
{
    xTaskCreate(gps_task, "gps_task", 1024 * 3, NULL, GPS_PRIORITY, &gps_handle);
    vTaskSuspend(gps_handle);
}

void gps_task_suspend(void)
{
    vTaskSuspend(gps_handle);
}

void gps_task_resume(void)
{
    vTaskResume(gps_handle);
}

void gps_get_coord(double *lat, double *lng, double *alt)
{
    *lat = gps_lat;
    *lng = gps_lng;
    *alt = gps_altitude;
}

void gps_get_data(uint16_t *year, uint8_t *month, uint8_t *day)
{
    *year = gps_year;
    *month = gps_month;
    *day = gps_day;
}

void gps_get_time(uint8_t *hour, uint8_t *minute, uint8_t *second)
{
    *hour = gps_hour;
    *minute = gps_minute;
    *second = gps_second;
}

void gps_get_satellites(uint32_t *vsat, int *hdop)
{
    *vsat = gps_vsat;   // Visible Satellites
    *hdop = gps_hdop;
}

void gps_get_speed(double *speed)
{
    *speed = gps_speed;
}

void gps_get_fix(uint8_t *fix)
{
    *fix = gps_fix;
}

/* clang-format on */
void displayInfo()
{
    if(bGPSDEBUG)
        Serial.print(F("Location: "));

    gps_fix = 1;

    if (gps.location.isValid())
    {
        gps_lat = gps.location.lat();
        gps_lng = gps.location.lng();
        gps_altitude = (int)gps.altitude.meters();
        if(gps_altitude < 0)
            gps_altitude = 0;

        if(bGPSDEBUG)
        {
            Serial.print(gps_lat, 6);
            Serial.print(F(","));
            Serial.print(gps_lng, 6);
        }
    }
    else
    {
        if(bGPSDEBUG)
            Serial.print(F("INVALID"));
    
        gps_fix = 0;
    }

    if(bGPSDEBUG)
        Serial.print(F("  Date/Time: "));

    if (gps.date.isValid())
    {
        gps_year = gps.date.year();
        gps_month = gps.date.month();
        gps_day = gps.date.day();

        if(bGPSDEBUG)
        {
            Serial.print(gps_month);
            Serial.print(F("/"));
            Serial.print(gps_day);
            Serial.print(F("/"));
            Serial.print(gps_year);
        }
    }
    else
    {
        if(bGPSDEBUG)
            Serial.print(F("INVALID"));

        gps_fix = 0;
    }

    if(bGPSDEBUG)
        Serial.print(F(" "));

    if (gps.time.isValid())
    {
        gps_hour = gps.time.hour();
        gps_minute = gps.time.minute();
        gps_second = gps.time.second();

        if(bGPSDEBUG)
        {
            if (gps_hour < 10)
                Serial.print(F("0"));
            Serial.print(gps_hour);
            Serial.print(F(":"));
            if (gps_minute < 10)
                Serial.print(F("0"));
            Serial.print(gps_minute);
            Serial.print(F(":"));
            if (gps_second < 10)
                Serial.print(F("0"));
            Serial.print(gps_second);
            Serial.print(F("."));
        }
    }
    else
    {
        if(bGPSDEBUG)
            Serial.print(F("INVALID"));
            
        gps_fix = 0;
    }

    if(bGPSDEBUG)
        Serial.print(F("  Satellites: "));

    if(gps.satellites.isValid())
    {
        gps_vsat = gps.satellites.value();
        gps_hdop = gps.hdop.value();

        if(bGPSDEBUG)
        {
            Serial.print(gps_vsat);
            Serial.print(F(" "));
        }
    }
    else
    {
        if(bGPSDEBUG)
            Serial.print(F("INVALID"));
            
        gps_vsat = 0;
        gps_hdop = 9999;
        gps_fix = 0;
    }


    if(bGPSDEBUG)
        Serial.print(F("  Speed: "));

    if(gps.speed.isValid())
    {
        gps_speed = gps.speed.kmph();

        if(bGPSDEBUG)
        {
            Serial.print(gps_speed);
            Serial.print(F(" "));
        }
    }
    else
    {
        if(bGPSDEBUG)
            Serial.print(F("INVALID"));
            
        gps_speed = 0;
        gps_fix = 0;
    }

    if(bGPSDEBUG)
        Serial.println();
}
/* clang-format off */

bool setupGPS()
{
    // L76K GPS USE 9600 BAUDRATE
    SerialGPS.begin(9600, SERIAL_8N1, BOARD_GPS_RXD, BOARD_GPS_TXD);
    bool result = false;
    uint32_t startTimeout ;
    for (int i = 0; i < 3; ++i) {
        SerialGPS.write("$PCAS03,0,0,0,0,0,0,0,0,0,0,,,0,0*02\r\n");
        delay(5);
        // Get version information
        startTimeout = millis() + 3000;
        Serial.print("Try to init L76K . Wait stop .");
        while (SerialGPS.available()) {
            Serial.print(".");
            SerialGPS.readString();
            if (millis() > startTimeout) {
                Serial.println("Wait L76K stop NMEA timeout!");
                return false;
            }
        };
        Serial.println();
        SerialGPS.flush();
        delay(200);

        SerialGPS.write("$PCAS06,0*1B\r\n");
        startTimeout = millis() + 500;
        String ver = "";
        while (!SerialGPS.available()) {
            if (millis() > startTimeout) {
                Serial.println("Get L76K timeout!");
                return false;
            }
        }
        SerialGPS.setTimeout(10);
        ver = SerialGPS.readStringUntil('\n');
        if (ver.startsWith("$GPTXT,01,01,02")) {
            Serial.println("L76K GNSS init succeeded, using L76K GNSS Module\n");
            result = true;
            break;
        }
        delay(500);
    }
    // Initialize the L76K Chip, use GPS + GLONASS
    SerialGPS.write("$PCAS04,5*1C\r\n");
    delay(250);
    SerialGPS.write("$PCAS03,1,1,1,1,1,1,1,1,1,1,,,0,0*26\r\n");
    delay(250);
    // Switch to Vehicle Mode, since SoftRF enables Aviation < 2g
    SerialGPS.write("$PCAS11,3*1E\r\n");
    return result;
}


static int getAck(uint8_t *buffer, uint16_t size, uint8_t requestedClass, uint8_t requestedID)
{
    uint16_t    ubxFrameCounter = 0;
    bool        ubxFrame = 0;
    uint32_t    startTime = millis();
    uint16_t    needRead;

    while (millis() - startTime < 800) {
        while (SerialGPS.available()) {
            int c = SerialGPS.read();
            switch (ubxFrameCounter) {
            case 0:
                if (c == 0xB5) {
                    ubxFrameCounter++;
                }
                break;
            case 1:
                if (c == 0x62) {
                    ubxFrameCounter++;
                } else {
                    ubxFrameCounter = 0;
                }
                break;
            case 2:
                if (c == requestedClass) {
                    ubxFrameCounter++;
                } else {
                    ubxFrameCounter = 0;
                }
                break;
            case 3:
                if (c == requestedID) {
                    ubxFrameCounter++;
                } else {
                    ubxFrameCounter = 0;
                }
                break;
            case 4:
                needRead = c;
                ubxFrameCounter++;
                break;
            case 5:
                needRead |=  (c << 8);
                ubxFrameCounter++;
                break;
            case 6:
                if (needRead >= size) {
                    ubxFrameCounter = 0;
                    break;
                }
                if (SerialGPS.readBytes(buffer, needRead) != needRead) {
                    ubxFrameCounter = 0;
                } else {
                    return needRead;
                }
                break;

            default:
                break;
            }
        }
    }
    return 0;
}

static bool GPS_Recovery()
{
    uint8_t cfg_clear1[] = {0xB5, 0x62, 0x06, 0x09, 0x0D, 0x00, 0xFF, 0xFF, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x02, 0x1C, 0xA2};
    uint8_t cfg_clear2[] = {0xB5, 0x62, 0x06, 0x09, 0x0D, 0x00, 0xFF, 0xFF, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x01, 0x1B, 0xA1};
    uint8_t cfg_clear3[] = {0xB5, 0x62, 0x06, 0x09, 0x0D, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0xFF, 0xFF, 0x00, 0x00, 0x03, 0x1D, 0xB3};
    SerialGPS.write(cfg_clear1, sizeof(cfg_clear1));

    if (getAck(buffer, 256, 0x05, 0x01)) {
        Serial.println("Get ack successes!");
    }
    SerialGPS.write(cfg_clear2, sizeof(cfg_clear2));
    if (getAck(buffer, 256, 0x05, 0x01)) {
        Serial.println("Get ack successes!");
    }
    SerialGPS.write(cfg_clear3, sizeof(cfg_clear3));
    if (getAck(buffer, 256, 0x05, 0x01)) {
        Serial.println("Get ack successes!");
    }

    // UBX-CFG-RATE, Size 8, 'Navigation/measurement rate settings'
    uint8_t cfg_rate[] = {0xB5, 0x62, 0x06, 0x08, 0x00, 0x00, 0x0E, 0x30};
    SerialGPS.write(cfg_rate, sizeof(cfg_rate));
    if (getAck(buffer, 256, 0x06, 0x08)) {
        Serial.println("Get ack successes!");
    } else {
        return false;
    }
    return true;
}
